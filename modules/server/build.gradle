plugins {
    id("java")
    id("application")
    id("io.freefair.lombok") version "8.0.1"
    id("org.openjfx.javafxplugin") version "0.0.10"
    id("org.beryx.jlink") version "2.24.1"
}

group "com.astrog"
version "1.1"

repositories {
    mavenCentral()
}

ext {
    junitVersion = "5.8.1"
}

sourceCompatibility = "17"
targetCompatibility = "17"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

application {
    mainModule = "com.astrog.shootergame"
    mainClass = "com.astrog.shootergame.MainApplication"
}

javafx {
    version = "17.0.1"
    modules = ["javafx.controls", "javafx.fxml"]
}

dependencies {
    implementation(project(":modules:common"))

    implementation("org.postgresql:postgresql:42.1.4")
    implementation("org.hibernate:hibernate-core:6.2.1.Final")

    implementation("org.projectlombok:lombok:1.18.26")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
    launcher {
        name = "app"
    }
}

jlinkZip {
    group = "distribution"
}

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": application.mainClass
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}